\section{\src{dyn_divdamp}}

\subsection{Description}

Kernel \src{dyn_divdamp} is taken from the original subroutine
\src{OPRT3D_divdamp} in \NICAM.
%
This subroutine is originally defined, as you can see in that name, in
\src{mod_oprt3d}.
%
Subroutine \src{OPRT3D_divdamp} calculates the gradient of divergence of
the vector $\{v_x, v_y, v_z\}$.
%
If this vector represents the velocity vector, this term is called
``divergence damping term''.


\subsection{Discretization and code}

Argument lists and local variables definition part of this subroutine is
as follows.

\begin{LstF90}[name=divdamp]
subroutine OPRT3D_divdamp( &
     ddivdx,    ddivdx_pl,    &
     ddivdy,    ddivdy_pl,    &
     ddivdz,    ddivdz_pl,    &
     rhogvx,    rhogvx_pl,    &
     rhogvy,    rhogvy_pl,    &
     rhogvz,    rhogvz_pl,    &
     rhogw,     rhogw_pl,     &
     coef_intp, coef_intp_pl, &
     coef_diff, coef_diff_pl  )
  implicit none

  real(RP), intent(out) :: ddivdx      (ADM_gall   ,ADM_kall,ADM_lall   ) ! tendency
  real(RP), intent(out) :: ddivdx_pl   (ADM_gall_pl,ADM_kall,ADM_lall_pl)
  real(RP), intent(out) :: ddivdy      (ADM_gall   ,ADM_kall,ADM_lall   )
  real(RP), intent(out) :: ddivdy_pl   (ADM_gall_pl,ADM_kall,ADM_lall_pl)
  real(RP), intent(out) :: ddivdz      (ADM_gall   ,ADM_kall,ADM_lall   )
  real(RP), intent(out) :: ddivdz_pl   (ADM_gall_pl,ADM_kall,ADM_lall_pl)
  real(RP), intent(in)  :: rhogvx      (ADM_gall   ,ADM_kall,ADM_lall   ) ! rho*vx { gam2 x G^1/2 }
  real(RP), intent(in)  :: rhogvx_pl   (ADM_gall_pl,ADM_kall,ADM_lall_pl)
  real(RP), intent(in)  :: rhogvy      (ADM_gall   ,ADM_kall,ADM_lall   ) ! rho*vy { gam2 x G^1/2 }
  real(RP), intent(in)  :: rhogvy_pl   (ADM_gall_pl,ADM_kall,ADM_lall_pl)
  real(RP), intent(in)  :: rhogvz      (ADM_gall   ,ADM_kall,ADM_lall   ) ! rho*vz { gam2 x G^1/2 }
  real(RP), intent(in)  :: rhogvz_pl   (ADM_gall_pl,ADM_kall,ADM_lall_pl)
  real(RP), intent(in)  :: rhogw       (ADM_gall   ,ADM_kall,ADM_lall   ) ! rho*w  { gam2 x G^1/2 }
  real(RP), intent(in)  :: rhogw_pl    (ADM_gall_pl,ADM_kall,ADM_lall_pl)
  real(RP), intent(in)  :: coef_intp   (ADM_gall   ,1:3,ADM_nxyz,TI:TJ,ADM_lall   )
  real(RP), intent(in)  :: coef_intp_pl(ADM_gall_pl,1:3,ADM_nxyz,      ADM_lall_pl)
  real(RP), intent(in)  :: coef_diff   (ADM_gall,1:6        ,ADM_nxyz,ADM_lall   )
  real(RP), intent(in)  :: coef_diff_pl(         1:ADM_vlink,ADM_nxyz,ADM_lall_pl)

  real(RP) :: sclt   (ADM_gall   ,TI:TJ) ! scalar on the hexagon vertex
  real(RP) :: sclt_pl(ADM_gall_pl)
  real(RP) :: sclt_rhogw
  real(RP) :: sclt_rhogw_pl

  real(RP) :: rhogvx_vm   (ADM_gall   )                      ! rho*vx / vertical metrics
  real(RP) :: rhogvx_vm_pl(ADM_gall_pl)
  real(RP) :: rhogvy_vm   (ADM_gall   )                      ! rho*vy / vertical metrics
  real(RP) :: rhogvy_vm_pl(ADM_gall_pl)
  real(RP) :: rhogvz_vm   (ADM_gall   )                      ! rho*vz / vertical metrics
  real(RP) :: rhogvz_vm_pl(ADM_gall_pl)
  real(RP) :: rhogw_vm    (ADM_gall,   ADM_kall,ADM_lall   ) ! rho*w  / vertical metrics
  real(RP) :: rhogw_vm_pl (ADM_gall_pl,ADM_kall,ADM_lall_pl)

  integer  :: gmin, gmax, iall, gall, kall, kmin, kmax, lall, gminm1

  integer  :: ij
  integer  :: ip1j, ijp1, ip1jp1
  integer  :: im1j, ijm1, im1jm1

  integer  :: g, k, l, n, v
  !---------------------------------------------------------------------------
\end{LstF90}

Here \src{ddivdx}, \src{ddivdy}, \src{ddivdz} are calculated x, y, z component
of the gradient of divergence, respectively.
%
And these with \src{_pl} are those for the pole region.
%
\src{rhogvx}, \src{rhogvy}, \src{rhogvz}, and \src{rhogw} are
$G^{1/2} \gamma^2  \times \rho v_x$,
$G^{1/2} \gamma^2  \times \rho v_y$,
$G^{1/2} \gamma^2  \times \rho v_z$, and
$G^{1/2} \gamma^2  \times \rho w$, respectively,
where $\{v_x, v_y, v_z\}$ are the wind vector of horizontal wind component in 3-D Cartesian
coordinates, $w$ is vertical wind,
and $G^{1/2}$ and $\gamma$ are the metrics comes from the
terrain-following coordinate described in \autoref{s:vert_coord}
%
Other arguments \src{coef_intp}, \src{coeff_diff} and those with
\src{_pl} are various coefficients for finite difference calculation,
the same with those in \src{dyn_diffusion}.

Local variable \src{sclt}, \src{sclt_pl} are the scalar value on the
gravitational cente of triangles, i.e. the vertices of the hexagonal control volume.
%
\src{rhogvx_vm} etc. are \src{rhogvx} divided by the vertical metrics.



The first part of this subroutine is as follows.

\begin{LstF90}[name=divdamp, firstnumber=last]
  call DEBUG_rapstart('OPRT3D_divdamp')

  gmin = (ADM_gmin-1)*ADM_gall_1d + ADM_gmin
  gmax = (ADM_gmax-1)*ADM_gall_1d + ADM_gmax
  iall = ADM_gall_1d
  gall = ADM_gall
  kall = ADM_kall
  kmin = ADM_kmin
  kmax = ADM_kmax
  lall = ADM_lall

  gminm1 = (ADM_gmin-1-1)*ADM_gall_1d + ADM_gmin-1

  !$omp parallel default(none),private(g,k,l), &
  !$omp shared(gall,kmin,kmax,lall,rhogw_vm,rhogvx,rhogvy,rhogvz,rhogw,VMTR_C2WfactGz,VMTR_RGSQRTH,VMTR_RGAMH)
  do l = 1, lall
     !$omp do
     do k = kmin+1, kmax
     do g = 1, gall
        rhogw_vm(g,k,l) = ( VMTR_C2WfactGz(g,k,1,l) * rhogvx(g,k  ,l) &
                          + VMTR_C2WfactGz(g,k,2,l) * rhogvx(g,k-1,l) &
                          + VMTR_C2WfactGz(g,k,3,l) * rhogvy(g,k  ,l) &
                          + VMTR_C2WfactGz(g,k,4,l) * rhogvy(g,k-1,l) &
                          + VMTR_C2WfactGz(g,k,5,l) * rhogvz(g,k  ,l) &
                          + VMTR_C2WfactGz(g,k,6,l) * rhogvz(g,k-1,l) &
                          ) * VMTR_RGAMH(g,k,l)                       & ! horizontal contribution
                        + rhogw(g,k,l) * VMTR_RGSQRTH(g,k,l)            ! vertical   contribution
     enddo
     enddo
     !$omp end do nowait

!OCL XFILL
     !$omp do
     do g = 1, gall
        rhogw_vm(g,kmin  ,l) = 0.0_RP
        rhogw_vm(g,kmax+1,l) = 0.0_RP
     enddo
     !$omp end do
  enddo
  !$omp end parallel

\end{LstF90}

This part calculates \src{rhogw_vm} from 3 components of $\rho \bm{v}$
and $\rho w$ (with the metrics).
The values are located at the triangluar points in horizonatal direction,
and at the half-integer levels in vertical direction.
%
Coefficients prefixed with \src{VMTR_} are defined and pre-calculated in
module \src{mod_vmtr} in original \NICAM.
%
When kernelize this subroutine, these definition are moved to
\src{mod_misc}, and read from the input data file.



Second part is pretty long as follows.

\begin{LstF90}[name=divdamp,firstnumber=last]
  !$omp parallel default(none),private(g,k,l,ij,ip1j,ip1jp1,ijp1,im1j,ijm1,im1jm1,sclt_rhogw), &
  !$omp shared(ADM_have_sgp,gminm1,gmin,gmax,gall,kmin,kmax,lall,iall,ddivdx,ddivdy,ddivdz,rhogvx,rhogvy,rhogvz, &
  !$omp        rhogvx_vm,rhogvy_vm,rhogvz_vm,rhogw_vm,sclt,coef_intp,coef_diff,GRD_rdgz,VMTR_RGAM)
  do l = 1, lall
     do k = kmin, kmax
!OCL XFILL
        !$omp do
        do g = 1, gall
           rhogvx_vm(g) = rhogvx(g,k,l) * VMTR_RGAM(g,k,l)
           rhogvy_vm(g) = rhogvy(g,k,l) * VMTR_RGAM(g,k,l)
           rhogvz_vm(g) = rhogvz(g,k,l) * VMTR_RGAM(g,k,l)
        enddo
        !$omp end do

        !$omp do
        do g = gminm1, gmax
           ij     = g
           ip1j   = g + 1
           ip1jp1 = g + iall + 1
           ijp1   = g + iall

           sclt_rhogw = ( ( rhogw_vm(ij,k+1,l) + rhogw_vm(ip1j,k+1,l) + rhogw_vm(ip1jp1,k+1,l) ) &
                        - ( rhogw_vm(ij,k  ,l) + rhogw_vm(ip1j,k  ,l) + rhogw_vm(ip1jp1,k  ,l) ) &
                        ) / 3.0_RP * GRD_rdgz(k)

           sclt(g,TI) = coef_intp(g,1,XDIR,TI,l) * rhogvx_vm(ij    ) &
                      + coef_intp(g,2,XDIR,TI,l) * rhogvx_vm(ip1j  ) &
                      + coef_intp(g,3,XDIR,TI,l) * rhogvx_vm(ip1jp1) &
                      + coef_intp(g,1,YDIR,TI,l) * rhogvy_vm(ij    ) &
                      + coef_intp(g,2,YDIR,TI,l) * rhogvy_vm(ip1j  ) &
                      + coef_intp(g,3,YDIR,TI,l) * rhogvy_vm(ip1jp1) &
                      + coef_intp(g,1,ZDIR,TI,l) * rhogvz_vm(ij    ) &
                      + coef_intp(g,2,ZDIR,TI,l) * rhogvz_vm(ip1j  ) &
                      + coef_intp(g,3,ZDIR,TI,l) * rhogvz_vm(ip1jp1) &
                      + sclt_rhogw
        enddo
        !$omp end do nowait

        !$omp do
        do g = gminm1, gmax
           ij     = g
           ip1j   = g + 1
           ip1jp1 = g + iall + 1
           ijp1   = g + iall

           sclt_rhogw = ( ( rhogw_vm(ij,k+1,l) + rhogw_vm(ip1jp1,k+1,l) + rhogw_vm(ijp1,k+1,l) ) &
                        - ( rhogw_vm(ij,k  ,l) + rhogw_vm(ip1jp1,k  ,l) + rhogw_vm(ijp1,k  ,l) ) &
                        ) / 3.0_RP * GRD_rdgz(k)

           sclt(g,TJ) = coef_intp(g,1,XDIR,TJ,l) * rhogvx_vm(ij    ) &
                      + coef_intp(g,2,XDIR,TJ,l) * rhogvx_vm(ip1jp1) &
                      + coef_intp(g,3,XDIR,TJ,l) * rhogvx_vm(ijp1  ) &
                      + coef_intp(g,1,YDIR,TJ,l) * rhogvy_vm(ij    ) &
                      + coef_intp(g,2,YDIR,TJ,l) * rhogvy_vm(ip1jp1) &
                      + coef_intp(g,3,YDIR,TJ,l) * rhogvy_vm(ijp1  ) &
                      + coef_intp(g,1,ZDIR,TJ,l) * rhogvz_vm(ij    ) &
                      + coef_intp(g,2,ZDIR,TJ,l) * rhogvz_vm(ip1jp1) &
                      + coef_intp(g,3,ZDIR,TJ,l) * rhogvz_vm(ijp1  ) &
                      + sclt_rhogw
        enddo
        !$omp end do

        if ( ADM_have_sgp(l) ) then ! pentagon
           !$omp master
           sclt(gminm1,TI) = sclt(gminm1+1,TJ)
           !$omp end master
        endif

!OCL XFILL
        !$omp do
        do g = 1, gmin-1
           ddivdx(g,k,l) = 0.0_RP
           ddivdy(g,k,l) = 0.0_RP
           ddivdz(g,k,l) = 0.0_RP
        enddo
        !$omp end do nowait

        !$omp do
        do g = gmin, gmax
           ij     = g
           im1j   = g - 1
           im1jm1 = g - iall - 1
           ijm1   = g - iall

           ddivdx(g,k,l) = ( coef_diff(g,1,XDIR,l) * ( sclt(ij,    TI) + sclt(ij,    TJ) ) &
                           + coef_diff(g,2,XDIR,l) * ( sclt(ij,    TJ) + sclt(im1j,  TI) ) &
                           + coef_diff(g,3,XDIR,l) * ( sclt(im1j,  TI) + sclt(im1jm1,TJ) ) &
                           + coef_diff(g,4,XDIR,l) * ( sclt(im1jm1,TJ) + sclt(im1jm1,TI) ) &
                           + coef_diff(g,5,XDIR,l) * ( sclt(im1jm1,TI) + sclt(ijm1  ,TJ) ) &
                           + coef_diff(g,6,XDIR,l) * ( sclt(ijm1,  TJ) + sclt(ij,    TI) ) )
        enddo
        !$omp end do nowait

        !$omp do
        do g = gmin, gmax
           ij     = g
           im1j   = g - 1
           im1jm1 = g - iall - 1
           ijm1   = g - iall

           ddivdy(g,k,l) = ( coef_diff(g,1,YDIR,l) * ( sclt(ij,    TI) + sclt(ij,    TJ) ) &
                           + coef_diff(g,2,YDIR,l) * ( sclt(ij,    TJ) + sclt(im1j,  TI) ) &
                           + coef_diff(g,3,YDIR,l) * ( sclt(im1j,  TI) + sclt(im1jm1,TJ) ) &
                           + coef_diff(g,4,YDIR,l) * ( sclt(im1jm1,TJ) + sclt(im1jm1,TI) ) &
                           + coef_diff(g,5,YDIR,l) * ( sclt(im1jm1,TI) + sclt(ijm1  ,TJ) ) &
                           + coef_diff(g,6,YDIR,l) * ( sclt(ijm1,  TJ) + sclt(ij,    TI) ) )
        enddo
        !$omp end do nowait

        !$omp do
        do g = gmin, gmax
           ij     = g
           im1j   = g - 1
           im1jm1 = g - iall - 1
           ijm1   = g - iall

           ddivdz(g,k,l) = ( coef_diff(g,1,ZDIR,l) * ( sclt(ij,    TI) + sclt(ij,    TJ) ) &
                           + coef_diff(g,2,ZDIR,l) * ( sclt(ij,    TJ) + sclt(im1j,  TI) ) &
                           + coef_diff(g,3,ZDIR,l) * ( sclt(im1j,  TI) + sclt(im1jm1,TJ) ) &
                           + coef_diff(g,4,ZDIR,l) * ( sclt(im1jm1,TJ) + sclt(im1jm1,TI) ) &
                           + coef_diff(g,5,ZDIR,l) * ( sclt(im1jm1,TI) + sclt(ijm1  ,TJ) ) &
                           + coef_diff(g,6,ZDIR,l) * ( sclt(ijm1,  TJ) + sclt(ij,    TI) ) )
        enddo
        !$omp end do nowait

!OCL XFILL
        !$omp do
        do g = gmax+1, gall
           ddivdx(g,k,l) = 0.0_RP
           ddivdy(g,k,l) = 0.0_RP
           ddivdz(g,k,l) = 0.0_RP
        enddo
        !$omp end do
     enddo ! loop k

!OCL XFILL
     !$omp do
     do g = 1, gall
        ddivdx(g,kmin-1,l) = 0.0_RP
        ddivdy(g,kmin-1,l) = 0.0_RP
        ddivdz(g,kmin-1,l) = 0.0_RP
        ddivdx(g,kmax+1,l) = 0.0_RP
        ddivdy(g,kmax+1,l) = 0.0_RP
        ddivdz(g,kmax+1,l) = 0.0_RP
     enddo
     !$omp end do
  enddo ! loop l
  !$omp end parallel

\end{LstF90}

In the outer most \src{l}-loop(l.98) and \src{k}-loop(l.99),
\src{sclt} at the triangular point \src{TI}(l.120) and \src{TJ}(l.144)
are calculated separately by interpolation.
%
and finally,
in the \src{g}-loop begins at l.173,
desired \src{ddivdx}(l.179), \src{ddivdy}(l.195), and
\src{ddivdz}(l.211) are calculated.


The final part is for the pole region, doing almost the same thing
as the normal region described above.

\begin{LstF90}[name=divdamp,firstnumber=last]
  if ( ADM_have_pl ) then
     n = ADM_gslf_pl

     do l = 1, ADM_lall_pl
        do k = ADM_kmin+1, ADM_kmax
        do g = 1, ADM_gall_pl
           rhogw_vm_pl(g,k,l) = ( VMTR_C2WfactGz_pl(g,k,1,l) * rhogvx_pl(g,k  ,l) &
                                + VMTR_C2WfactGz_pl(g,k,2,l) * rhogvx_pl(g,k-1,l) &
                                + VMTR_C2WfactGz_pl(g,k,3,l) * rhogvy_pl(g,k  ,l) &
                                + VMTR_C2WfactGz_pl(g,k,4,l) * rhogvy_pl(g,k-1,l) &
                                + VMTR_C2WfactGz_pl(g,k,5,l) * rhogvz_pl(g,k  ,l) &
                                + VMTR_C2WfactGz_pl(g,k,6,l) * rhogvz_pl(g,k-1,l) &
                                ) * VMTR_RGAMH_pl(g,k,l)                          & ! horizontal contribution
                              + rhogw_pl(g,k,l) * VMTR_RGSQRTH_pl(g,k,l)            ! vertical   contribution
        enddo
        enddo

        do g = 1, ADM_gall_pl
           rhogw_vm_pl(g,ADM_kmin  ,l) = 0.0_RP
           rhogw_vm_pl(g,ADM_kmax+1,l) = 0.0_RP
        enddo
     enddo

     do l = 1, ADM_lall_pl
        do k = ADM_kmin, ADM_kmax
           do v = 1, ADM_gall_pl
              rhogvx_vm_pl(v) = rhogvx_pl(v,k,l) * VMTR_RGAM_pl(v,k,l)
              rhogvy_vm_pl(v) = rhogvy_pl(v,k,l) * VMTR_RGAM_pl(v,k,l)
              rhogvz_vm_pl(v) = rhogvz_pl(v,k,l) * VMTR_RGAM_pl(v,k,l)
           enddo

           do v = ADM_gmin_pl, ADM_gmax_pl
              ij   = v
              ijp1 = v + 1
              if( ijp1 == ADM_gmax_pl+1 ) ijp1 = ADM_gmin_pl

              sclt_rhogw_pl = ( ( rhogw_vm_pl(n,k+1,l) + rhogw_vm_pl(ij,k+1,l) + rhogw_vm_pl(ijp1,k+1,l) ) &
                              - ( rhogw_vm_pl(n,k  ,l) + rhogw_vm_pl(ij,k  ,l) + rhogw_vm_pl(ijp1,k  ,l) ) &
                              ) / 3.0_RP * GRD_rdgz(k)

              sclt_pl(ij) = coef_intp_pl(v,1,XDIR,l) * rhogvx_vm_pl(n   ) &
                          + coef_intp_pl(v,2,XDIR,l) * rhogvx_vm_pl(ij  ) &
                          + coef_intp_pl(v,3,XDIR,l) * rhogvx_vm_pl(ijp1) &
                          + coef_intp_pl(v,1,YDIR,l) * rhogvy_vm_pl(n   ) &
                          + coef_intp_pl(v,2,YDIR,l) * rhogvy_vm_pl(ij  ) &
                          + coef_intp_pl(v,3,YDIR,l) * rhogvy_vm_pl(ijp1) &
                          + coef_intp_pl(v,1,ZDIR,l) * rhogvz_vm_pl(n   ) &
                          + coef_intp_pl(v,2,ZDIR,l) * rhogvz_vm_pl(ij  ) &
                          + coef_intp_pl(v,3,ZDIR,l) * rhogvz_vm_pl(ijp1) &
                          + sclt_rhogw_pl
           enddo

           ddivdx_pl(:,k,l) = 0.0_RP
           ddivdy_pl(:,k,l) = 0.0_RP
           ddivdz_pl(:,k,l) = 0.0_RP

           do v = ADM_gmin_pl, ADM_gmax_pl
              ij   = v
              ijm1 = v - 1
              if( ijm1 == ADM_gmin_pl-1 ) ijm1 = ADM_gmax_pl ! cyclic condition

              ddivdx_pl(n,k,l) = ddivdx_pl(n,k,l) + coef_diff_pl(v-1,XDIR,l) * ( sclt_pl(ijm1) + sclt_pl(ij) )
              ddivdy_pl(n,k,l) = ddivdy_pl(n,k,l) + coef_diff_pl(v-1,YDIR,l) * ( sclt_pl(ijm1) + sclt_pl(ij) )
              ddivdz_pl(n,k,l) = ddivdz_pl(n,k,l) + coef_diff_pl(v-1,ZDIR,l) * ( sclt_pl(ijm1) + sclt_pl(ij) )
           enddo
        enddo

        ddivdx_pl(:,ADM_kmin-1,l) = 0.0_RP
        ddivdx_pl(:,ADM_kmax+1,l) = 0.0_RP
        ddivdy_pl(:,ADM_kmin-1,l) = 0.0_RP
        ddivdy_pl(:,ADM_kmax+1,l) = 0.0_RP
        ddivdz_pl(:,ADM_kmin-1,l) = 0.0_RP
        ddivdz_pl(:,ADM_kmax+1,l) = 0.0_RP
     enddo
  else
     ddivdx_pl(:,:,:) = 0.0_RP
     ddivdy_pl(:,:,:) = 0.0_RP
     ddivdz_pl(:,:,:) = 0.0_RP
  endif

  call DEBUG_rapend('OPRT3D_divdamp')

  return
end subroutine OPRT3D_divdamp
\end{LstF90}




\subsection{Input data and result}

Max/min/sum of input/output data of the kernel subroutine are output as
a log.
%
Below is an example of \src{$IAB_SYS=Ubuntu-gnu-ompi} case.

\begin{LstLog}
 ### Input ###
 +check[check_ddivdx    ] max=  3.0131744015374420E-11,min= -5.2988229203876260E-11,sum= -1.6975403236890680E-11
 +check[check_ddivdx_pl ] max=  2.6717761683260969E-11,min= -5.3219224048058505E-11,sum=  4.8275913249408648E-11
 +check[check_ddivdy    ] max=  2.8051192990274010E-11,min= -2.3223214708700599E-11,sum= -4.5483269668124264E-11
 +check[check_ddivdy_pl ] max=  4.4686835079822753E-11,min= -4.1286060234480353E-11,sum=  6.8817604250940581E-11
 +check[check_ddivdz    ] max=  3.4380463226247279E-11,min= -1.3010875841849350E-11,sum= -7.2052908719075937E-11
 +check[check_ddivdz_pl ] max=  3.2276474319270210E-13,min= -8.5477300588035169E-14,sum=  3.9437219768394675E-12
 +check[rhogvx          ] max=  2.2205470252374623E-01,min= -2.0636880346843767E-01,sum= -1.7381945003843336E+02
 +check[rhogvx_pl       ] max=  1.4368235944041274E-01,min= -1.9062928892442133E-01,sum= -5.6937904377485706E+00
 +check[rhogvy          ] max=  2.4120815977222165E-01,min= -2.5907366818620919E-01,sum=  1.7927398128296358E+02
 +check[rhogvy_pl       ] max=  8.8938663283927272E-02,min= -9.6299249362234787E-02,sum= -9.3645167747781997E-03
 +check[rhogvz          ] max=  2.0899984486710360E-01,min= -1.9461784957198358E-01,sum=  2.9192900535497301E+02
 +check[rhogvz_pl       ] max=  1.2715227826262342E-03,min= -6.6561175383069663E-04,sum=  2.2146129568583188E-02
 +check[rhogw           ] max=  1.2675708942695349E-03,min= -4.2678569215437437E-03,sum= -2.6525715444058587E-02
 +check[rhogw_pl        ] max=  9.9481836269239257E-04,min= -4.2678569215437437E-03,sum= -8.3801034534239580E-02
 ### Output ###
 +check[ddivdx          ] max=  3.0131744015374420E-11,min= -5.2988229203876260E-11,sum= -1.6975403236890680E-11
 +check[ddivdx_pl       ] max=  2.6717761683260969E-11,min= -5.3219224048058505E-11,sum=  4.8275913249408648E-11
 +check[ddivdy          ] max=  2.8051192990274010E-11,min= -2.3223214708700599E-11,sum= -4.5483269668124264E-11
 +check[ddivdy_pl       ] max=  4.4686835079822753E-11,min= -4.1286060234480353E-11,sum=  6.8817604250940581E-11
 +check[ddivdz          ] max=  3.4380463226247279E-11,min= -1.3010875841849350E-11,sum= -7.2052908719075937E-11
 +check[ddivdz_pl       ] max=  3.2276474319270210E-13,min= -8.5477300588035169E-14,sum=  3.9437219768394675E-12
 ### Validation : grid-by-grid diff ###
 +check[check_ddivdx    ] max=  0.0000000000000000E+00,min=  0.0000000000000000E+00,sum=  0.0000000000000000E+00
 +check[check_ddivdx_pl ] max=  0.0000000000000000E+00,min=  0.0000000000000000E+00,sum=  0.0000000000000000E+00
 +check[check_ddivdy    ] max=  0.0000000000000000E+00,min=  0.0000000000000000E+00,sum=  0.0000000000000000E+00
 +check[check_ddivdy_pl ] max=  0.0000000000000000E+00,min=  0.0000000000000000E+00,sum=  0.0000000000000000E+00
 +check[check_ddivdz    ] max=  0.0000000000000000E+00,min=  0.0000000000000000E+00,sum=  0.0000000000000000E+00
 +check[check_ddivdz_pl ] max=  0.0000000000000000E+00,min=  0.0000000000000000E+00,sum=  0.0000000000000000E+00
 *** Finish kernel
\end{LstLog}

Check the lines below \src{``Validation : grid-by-grid diff''} line,
that shows difference between calculated output array and
pre-calculated reference array.
These should be zero or enough small to be acceptable.

There are sample output log files in \file{reference/}
in each kernel program directory, for reference purpose.


\subsection{Sample of perfomance result}


Here's an example of the performance result part of the log output.
Below is an example executed with the machine environment described in \autoref{s:measuring_env}.
%
Note that in this program kernel part is iterated one time.

\begin{LstLog}
 *** Computational Time Report
 *** ID=001 : MAIN_dyn_divdamp                 T=     0.028 N=      1
 *** ID=002 : OPRT3D_divdamp                   T=     0.028 N=      1
\end{LstLog}

